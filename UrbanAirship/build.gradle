buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.+'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'robolectric'


repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"


    lintOptions {
        ignore 'GradleDependency'
    }

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 22

        buildConfigField "String", "URBAN_AIRSHIP_VERSION", "\"${airshipVersion}\""

        consumerProguardFiles 'consumer-proguard.txt'
    }

    sourceSets {
        androidTest.setRoot("src/test")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

robolectric {
    // configure the set of classes for JUnit tests
    include '**/*Test.class'

    // configure max heap size of the test JVM
    maxHeapSize = '2048m'

    // configure the test JVM arguments
    jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'

    // configure whether failing tests should fail the build
    ignoreFailures true

    // use afterTest to listen to the test execution results
    afterTest { descriptor, result ->
        println "Executing test for ${descriptor.name} with result: ${result.resultType}"
    }
}

// work around for provided dependencies as jars being included in the exploded aar
// https://code.google.com/p/android/issues/detail?id=69737
android.libraryVariants.all { variant ->
    variant.outputs.each { output ->
        output.packageLibrary.exclude('libs/amazon-device-messaging-1.0.1.jar')
    }
}

dependencies {
    androidTestCompile 'junit:junit:4.10', 'org.mockito:mockito-core:1.9.5', 'org.robolectric:robolectric:2.4'

    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.google.android.gms:play-services-gcm:7.0.0'

    provided files('libs/amazon-device-messaging-1.0.1.jar')
    provided 'com.android.support:support-v4:22.0.0'
    provided 'com.android.support:cardview-v7:22.0.0'

}

//generates NativeBridge.java
task templatizeNativeBridge(type: Exec) {
    commandLine 'src/main/js/templatize'
}

project.afterEvaluate {
    prepareDebugDependencies.dependsOn("templatizeNativeBridge")
    prepareReleaseDependencies.dependsOn("templatizeNativeBridge")
}
