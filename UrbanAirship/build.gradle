buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.+'
    }
}

apply plugin: 'com.android.library'


repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    lintOptions {
        ignore 'GradleDependency'
    }

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 22

        buildConfigField "String", "URBAN_AIRSHIP_VERSION", "\"${airshipVersion}\""

        consumerProguardFiles 'consumer-proguard.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.google.android.gms:play-services-gcm:7.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'

    provided files('libs/amazon-device-messaging-1.0.1.jar')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'org.robolectric:shadows-support-v4:3.0-rc2'
}

//generates NativeBridge.java
task templatizeNativeBridge(type: Exec) {
    commandLine 'src/main/js/templatize'
}

project.afterEvaluate {
    prepareDebugDependencies.dependsOn("templatizeNativeBridge")
    prepareReleaseDependencies.dependsOn("templatizeNativeBridge")
}
